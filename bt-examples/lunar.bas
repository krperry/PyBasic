0 REM THIS PROGRAM WAS MODIFIED FROM APPLESOFT BASIC FOR THE BNS BASIC COMPILER
5 REM BY RICHARD EHRLER, COMPUSERVE 76645,2710
10 ALLCAPS
20 PRINT "LUNAR LANDER"
30 PRINT "YOU ARE LANDING ON THE MOON AND HAVE TAKEN OVER MANUAL CONTROL 500 FEET ABOVE THE SURFACE OF THE MOON"
40 PRINT "YOU HAVE A DOWNWARD VELOCITY OF 50 FEET PER SECOND, AND YOU HAVE 120 UNITS OF FUEL REMAINING."
50 PRINT "YOUR OBJECTIVE IS TOO LAND SOFTLY ON THE MOON AND STILL HAVE FUEL REMAINING."
60 PRINT "TO DO THIS YOU MUST BURN FUEL AT A RATE WHICH WILL SLOW YOU DOWN, BUT NOT EXHAUST THE FUEL TOO EARLY!"
70 PRINT "UNFORTUNATELY, YOU CAN NOT BURN FUEL AT A CONSTANT RATE AND ACCOMPLISH YOUR OBJECTIVE."
80 PRINT "GOOD LUCK!"
90 B = 0: T = 0: H = 500: V = 50: F = 120
100 GOSUB 500
110 PRINT "BURN FUEL AT RATE?": INPUT B
120 IF B = 99 THEN GOSUB 530: GOTO 110
130 IF B < 0 THEN 260
140 IF B > 30 THEN B = 30
150 IF B > F THEN B = F
160 V1 = V - B + 5
170 F = F - B
180 H = H - .5 * (V + V1)
190 IF H <= 0 THEN GOTO 280
200 T = T + 1
210 V = V1
220 IF F > 0 THEN 100
230 IF B = 0 then GOTO 250
240 PRINT "OUT OF FUEL!"
250 GOSUB 500
260 B = 0
270 GOTO 160
280 PRINT "CONTACT!"
290 H = H + .5 * (V + V1)
300 IF B = 5 THEN GOTO 330
310 Z = pow ( (V * V + H * (10 - 2 * B)),  .5): D = ( - V + Z ) / (5 - B)
320 GOTO 340
330 D = H / V
340 V1 = V + (5 - B) * D
350 PRINT "TOUCH DOWN AT ";T + .01 * INT (100 * D);" SECONDS"
360 PRINT "FUEL REMAINING ";F;"UNITS"
370 PRINT "LANDING VELOCITY ";.01 * INT (100 * V1);" FEET/SECOND"
380 IF ABS (V1) < 5 THEN PRINT "PERFECT LANDING, CONGRATULATIONS!"
390 IF ABS (V1) > 10 THEN GOTO 420
400 PRINT "YOUR LICENSE WILL BE RENEWED LATER"
410 GOTO 440
420 PRINT "YOU REALLY BLEW IT THIS TIME, ACE!"
430 PRINT "REGRETS WILL BE SENT TO YOUR FAMILY"
440 PRINT "ANOTHER FLIGHT ? ";: GET A$
450 IF A$ = "Y" THEN GOTO  90
460 PRINT "ROGER, FRIEND CONTROL OUT."
470 END
500 IF F <= 0 then GOTO 530
510 click
520 B = B - 1: IF B > 0 then GOTO 510
530 PRINT "TIME " T
535 FOR TD = 1 TO 50: NEXT TD
540 PRINT "ALTITUDE ";.01 * INT (100 * H)
545 FOR TD = 1 TO 99: NEXT TD
550 PRINT "SPEED ";.01 * INT (100 * V)
555 FOR TD = 1 TO 75: NEXT TD
560 PRINT "FUEL ";.01 * INT(100 * F)
565 FOR TD = 1 TO 75: NEXT TD
570 RETURN
